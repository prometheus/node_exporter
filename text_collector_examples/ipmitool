#!/usr/bin/awk -f

#
# Converts output of `ipmitool sensor` to prometheus format.
#
# e.g.:
#   ipmitool sensor | ./ipmitool > ipmitool.prom
#

function discrete2float(hex_value) {
	return hex_value + 0;
}

# Fields are Bar separated, with space padding
BEGIN {
	FS = "[ ]*[|][ ]*";
}

# Not a valid line
{
	if (NF < 3) {
		next
	}
}

# $2 is value field
$2 ~ /na/ {
	next
}

# $3 is type field
$3 ~ /degrees C/ {
	printf("node_physical_temperature_celcius{sensor=\"%s\"} %f\n", $1, $2);
}

$3 ~ /Volts/ {
	printf("node_physical_volts{sensor=\"%s\"} %f\n", $1, $2);
}

$3 ~ /Watts/ {
	printf("node_physical_watts{sensor=\"%s\"} %f\n", $1, $2);
}

$3 ~ /RPM/ {
	printf("node_physical_speed_rpm{sensor=\"%s\"} %f\n", $1, $2);
}

$3 ~ /discrete/ {
	printf("node_physical_status{sensor=\"%s\"} %f\n", $1, discrete2float($2));
}
